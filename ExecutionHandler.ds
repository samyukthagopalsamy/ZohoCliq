/*
On typing the /medium command on the chat window, the user suggestions of -@Username and -Publication pops up.
*/
flag = false;
message = Map();
if(arguments == "") // The user is prompted to choose either of the suggestions.
{
	message.put("text","Please choose from the _options_ and enter something you want search as input. Usage \"/medium *option* free-code-camp\"");
	return message;
}
/*
https://rss2json.com/, is an online rss to json convertor used to convert rss feed from medium.com to json format.
*/
if(selections.size() > 0)
{
	details = selections.get(0).toMap();
	if(details.get("title").containsIgnoreCase("Username"))
	{
		url = "https://api.rss2json.com/v1/api.json?rss_url=https://medium.com/feed/@" + encodeUrl(arguments);
		url = url.removeFirstOccurence("+");
		flag = true;
	}
	else if(details.get("title").containsIgnoreCase("Publication"))
	{
		url = "https://api.rss2json.com/v1/api.json?rss_url=https://medium.com/feed/" + encodeUrl(arguments);
		url = url.removeFirstOccurence("+");
	}
}
else
{
	url = "https://api.rss2json.com/v1/api.json?rss_url=https://medium.com/feed/" + encodeUrl(arguments);
}

response = getUrl(url,{"Content-Encoding":"gzip"});
status = response.toMap().get("status");
/*
If the status returned in json is "ok" then the required keys are obtained from json.
*/
if(status == "ok")
{
	itemsList = response.toMap().get("items");
	feed = response.toMap().get("feed");
	title = feed.toMap().get("title");
	image = feed.toMap().get("image");
	slidesList = list();
	slidesList0 = Map();
	slidesList0.put("type","images");
	dataList = list();
	dataList.add(image);
	slidesList0.put("data",dataList);
	slidesList.add(slidesList0);
	message.put("slides",slidesList);
	text = "*There are no " + title + "*\n";
	if(itemsList.isEmpty())
	{
		message.put("text",text);
		return message;
	}
	count = 0;
	response = "*" + title + "*\n";
	indexes = {"1ï¸âƒ£","2ï¸âƒ£","3ï¸âƒ£","4ï¸âƒ£","5ï¸âƒ£","6ï¸âƒ£","7ï¸âƒ£","8ï¸âƒ£","9ï¸âƒ£","ğŸ”Ÿ"};// displaying first 10 items 
	for each  item in itemsList
	{
		l = item.get("link");
		link = l.replaceFirst("@","%40"); // converting special character @ to %40 for proper rendering of url.
		response = response + indexes.get(count) + "  [" + item.get("title") + "](" + link + ")    " + "\n";
		count = count + 1;
		if(count > 9)
		{
			break;
		}
	}
	message.put("text",response);
	return message;
}
else if(flag == true)
{
 /*
 use google custom search api to search medium.com specifically.
 */
	url = "https://www.googleapis.com/customsearch/v1?key={YOUR KEY}&cx=002084031288054668458:vcly9iguf3p&q=" + encodeUrl(arguments);
	response = getUrl(url,{"Content-Encoding":"gzip"});
	itemsList = "";
	res = response.toMap().get("searchInformation");
	r = res.get("totalResults");
	if(r == 0)
	{
		message.put("text","*No such _User/Publication_ exists in medium*");
		return message;
	}
	itemsList = response.toMap().get("items");
	count = 0;
	response = "*User name, not found! Maybe try any of these.*" + "\n";
	indexes = {"1ï¸âƒ£","2ï¸âƒ£","3ï¸âƒ£","4ï¸âƒ£","5ï¸âƒ£"};
	for each  item in itemsList
	{
		l = item.get("link");
		result = l.removeFirstOccurence("https://medium.com/@");
		response = response + indexes.get(count) + result + "\n";
		count = count + 1;
		if(count > 4)
		{
			break;
		}
	}
	message.put("text",response);
	return message;
}
else
{
	response = "*Publication name, not found in medium! Enter a valid publication *" + "\n";
	message.put("text",response);
	return message;
}
